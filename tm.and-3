#n

# Accepts tape: binary and operation
# Example: echo '0110#1100' | sed -rf tm.and-3

# This source code is released under MIT License
# Copyright (c) 2016 Peter Cerman (https://github.com/pcerman)

/^>/ !s/^/>/


#---------------------------------------------------------------------
:S0
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>0/    { s/>0/o>/; b S1 }
/>1/    { s/>1/I>/; b S5 }

b error

#---------------------------------------------------------------------
:S1
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[01]/ { s/>(.)/\1>/; b S1 }
/>#/    { s/>#/#>/; b S2 }

b error

#---------------------------------------------------------------------
:S2
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[oI]/ { s/>(.)/\1>/; b S2 }
/>0/    { s/>0/o>/; b S3 }
/>1/    { s/>1/I>/; b S3 }

b error

#---------------------------------------------------------------------
:S3
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g


/>[01]/ { s/>(.)/\1>/; b S3 }
/>#/    { s/>#/#>/; b S4 }
/>$/    { s/>$/#>/; b S4 }

b error

#---------------------------------------------------------------------
:S4
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[01]/ { s/>(.)/\1>/; b S4 }
/.>#/   { s/(.)>/>\10/; b S9 }
/.>$/   { s/(.)>/>\10/; b S9 }

b error

#---------------------------------------------------------------------
:S5
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[01]/ { s/>(.)/\1>/; b S5 }
/>#/    { s/>#/#>/; b S6 }

b error

#---------------------------------------------------------------------
:S6
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[oI]/ { s/>(.)/\1>/; b S6 }
/>0/    { s/>0/o>/; b S3 }
/>1/    { s/>1/I>/; b S7 }

b error

#---------------------------------------------------------------------
:S7
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g


/>[01]/ { s/>(.)/\1>/; b S7 }
/>#/    { s/>#/#>/; b S8 }
/>$/    { s/>$/#>/; b S8 }

b error

#---------------------------------------------------------------------
:S8
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[01]/ { s/>(.)/\1>/; b S8 }
/.>#/   { s/(.)>/>\11/; b S9 }
/.>$/   { s/(.)>/>\11/; b S9 }

b error

#---------------------------------------------------------------------
:S9
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/.>[01]/  { s/(.)>/>\1/; b S9 }
/.>#/     { s/(.)>/>\1/; b S10 }
/.>$/     { s/(.)>/>\1/; b S10 }

b error

#---------------------------------------------------------------------
:S10
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/.>[01oI]/  { s/(.)>/>\1/; b S10 }
/.>#/       { s/(.)>/>\1/; b S11 }

b error

#---------------------------------------------------------------------
:S11
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/.>[01]/  { s/(.)>/>\1/; b S12 }
/>[oI]/   { s/>(.)/\1>/; b S13 }

b error

#---------------------------------------------------------------------
:S12
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/.>[01]/  { s/(.)>/>\1/; b S12 }
/>[oI]/   { s/>(.)/\1>/; b S0 }

b error

#---------------------------------------------------------------------
:S13
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/.>#/     { s/(.)>/>\1/; b S13 }
/.>o/     { s/(.)>o/>\10/; b S13 }
/.>I/     { s/(.)>I/>\11/; b S13 }
/^>o/     { s/>o/0>/; b S14 }
/^>I/     { s/>I/1>/; b S14 }

b error

#---------------------------------------------------------------------
:S14
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[01]/   { s/>(.)/\1>/; b S14 }
/>#/      { s/>(.)/\1>/; b S15 }

b error

#---------------------------------------------------------------------
:S15
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>o/      { s/>o/0>/; b S15 }
/>I/      { s/>I/1>/; b S15 }
/>#/      { s/>#/#>/; b halt }

b error

#---------------------------------------------------------------------
:break
s/^/BREAK: /
p
q 2

#---------------------------------------------------------------------
:error
i \

s/.*/NOT ACCEPTED/
p
q 1

#---------------------------------------------------------------------
:halt
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

s/.*/ACCEPTED/
i \

p
q 0
