#n

# Accepts tape: binary number
# echo '101' | sed -rf tm.cnt2

# This source code is released under MIT License
# Copyright (c) 2016 Peter Cerman (https://github.com/pcerman)

/^>/ !s/^/>/
s/^/00:#/

b start

#---------------------------------------------------------------------
:S0
#---------------------------------------------------------------------

/>[01]/   { s/^..:/R00:/; b next }
/>#/      { s/^..:/L01:/; b next }

b error

#---------------------------------------------------------------------
:S1
#---------------------------------------------------------------------

/>1/      { s/>./>0/
            s/^..:/R02:/; b next }
/>0/      { s/>./>1/
            s/^..:/L01:/; b next }

b halt

#---------------------------------------------------------------------
:S2
#---------------------------------------------------------------------

/>[01]/   { s/^..:/R02:/; b next }
/>#/      { s/^..:/R03:/; b next }

b error

#---------------------------------------------------------------------
:S3
#---------------------------------------------------------------------

/>o/      { s/^..:/R03:/; b next }
/>#/      { s/>#/>o/
            s/^..:/L04:/; b next }

b error

#---------------------------------------------------------------------
:S4
#---------------------------------------------------------------------

/>o/      { s/^..:/L04:/; b next }
/>#/      { s/^..:/L01:/; b next }

b error

#*********************************************************************
#*********************************************************************
:next

/^L.*[^:]>/ { s/^L//; s/(.)>/>\1/; b jump }
/^R.*>../   { s/^R//; s/>(.)/\1>/; b jump }
/^R.*>.$/   { s/^R//; s/>(.)/\1>#/; b jump }
/^R.*>$/    { s/^R//; s/>/#>#/; b jump }

s/^[LR]//

b error

#*********************************************************************
:start
:jump

h;s/>.*/>/;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/^00:/  b S0
/^01:/  b S1
/^02:/  b S2
/^03:/  b S3
/^04:/  b S4
#/^05:/  b S5
#/^06:/  b S6
#/^07:/  b S7
#/^08:/  b S8
#/^09:/  b S9
#/^10:/  b S10

i \

s/^/UNKNOWN STATE: /
p
q 3

#*********************************************************************
:break
i\

s/^/BREAK: /
p
q 2

#*********************************************************************
:error
i \

s/.*/NOT ACCEPTED/
p
q 1

#*********************************************************************
:halt
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

s/.*/ACCEPTED/
i \

p
q 0
