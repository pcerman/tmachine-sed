#n

# Accepts tape: binary and operation
# Example: echo '1100#0110' | sed -rf tm.and-2

# This source code is released under MIT License
# Copyright (c) 2016 Peter Cerman (https://github.com/pcerman)

/^>/ !s/^/>/


#-------------------------------------------------------
:S1
#-------------------------------------------------------
s/^/ 1:  /
p
s/^.{5}//


/>1/ {
     s/>1/i>/
     b S2
}

/>0/ {
     s/>0/o>/
     b S3
}

b error


#-------------------------------------------------------
:S2
#-------------------------------------------------------
s/^/ 2:  /
p
s/^.{5}//


/>[10]/ {
     s/>(.)/\1>/
     b S2
}

/>#/ {
     s/>#/#>/
     b S4
}

b error


#-------------------------------------------------------
:S3
#-------------------------------------------------------
s/^/ 3:  /
p
s/^.{5}//


/>[10]/ {
     s/>(.)/\1>/
     b S3
}

/>#/ {
     s/>#/#>/
     b S5
}

b error


#-------------------------------------------------------
:S4
#-------------------------------------------------------
s/^/ 4:  /
p
s/^.{5}//


/>[io]/ {
     s/>(.)/\1>/
     b S4
}

/.>1/ {
     s/(.)>1/>\1i/
     b S6
}

/.>0/ {
     s/(.)>0/>\1o/
     b S6
}

#/>$/ b accept

b error


#-------------------------------------------------------
:S5
#-------------------------------------------------------
s/^/ 5:  /
p
s/^.{5}//


/>[io]/ {
     s/>(.)/\1>/
     b S5
}

/.>1/ {
     s/(.)>1/>\1o/
     b S6
}

/.>0/ {
     s/(.)>0/>\1o/
     b S6
}

#/>$/ b accept

b error


#-------------------------------------------------------
:S6
#-------------------------------------------------------
s/^/ 6:  /
p
s/^.{5}//


/.>[io]/ {
     s/(.)>(.)/>\1\2/
     b S6
}

/.>#/ {
     s/(.)>#/>\1#/
     b S7
}

b error


#-------------------------------------------------------
:S7
#-------------------------------------------------------
s/^/ 7:  /
p
s/^.{5}//


/>[io]/ {
     s/>(.)/\1>/
     b S9
}

/.>[10]/ {
     s/(.)>(.)/>\1\2/
     b S8
}

b error


#-------------------------------------------------------
:S8
#-------------------------------------------------------
s/^/ 8:  /
p
s/^.{5}//


/.>[10]/ {
     s/(.)>(.)/>\1\2/
     b S8
}

/>[io]/ {
     s/>(.)/\1>/
     b S1
}

b error


#-------------------------------------------------------
:S9
#-------------------------------------------------------
s/^/ 9:  /
p
s/^.{5}//


/>#/ {
     s/>#/#>/
     b S10
}

b error


#-------------------------------------------------------
:S10
#-------------------------------------------------------
s/^/10:  /
p
s/^.{5}//


/>o/ {
     s/>o/0>/
     b S10
}

/>i/ {
     s/>i/1>/
     b S10
}

/>$/ b accept

b error


#-------------------------------------------------------
:accept

i\

s/^/     /

a\
\
accepted

b end

#-------------------------------------------------------
:error

i\

s/^/     /

a\
\
invalid input

:end
