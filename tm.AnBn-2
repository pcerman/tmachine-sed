#n

# Accepts tape: a{n}b{n}
# Example: echo 'aabb' | sed -rf tm.AnBn-2

# This source code is released under MIT License
# Copyright (c) 2016 Peter Cerman (https://github.com/pcerman)

/^>/ !s/^/>/


#---------------------------------------------------------------------
:S0
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>a/ { s/>a/#>/;b S1 }

/>#/ { s/>#/#>/;b S4 }
/>$/ b S4

b error

#---------------------------------------------------------------------
:S1
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>[ab]/ { s/>(.)/\1>/;b S1 }

/.>#/ { s/(.)>#/>\1#/;b S2 }
/.>$/ { s/(.)>/>\1/; b S2 }

b error

#---------------------------------------------------------------------
:S2
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/.>b/ { s/(.)>b/>\1#/;b S3 }

b error

#---------------------------------------------------------------------
:S3
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/.>[ab]/ { s/(.)>(.)/>\1\2/;b S3 }

/>#/ { s/>#/#>/;b S0 }

b error

#---------------------------------------------------------------------
:S4
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>#/ { s/>#/#>/;b S5 }

/>$/ b halt

b error

#---------------------------------------------------------------------
:S5
#---------------------------------------------------------------------
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

/>#/ { s/>#/#>/;b halt }

/>$/ b halt

b error

#---------------------------------------------------------------------
:break
s/^/BREAK: /
p
q 2

#---------------------------------------------------------------------
:error
i \

s/.*/NOT ACCEPTED/
p
q 1

#---------------------------------------------------------------------
:halt
h;s/[^>]/ /g;s/>/_/;p;g;s/>//;p;g

s/.*/ACCEPTED/
i \

p
q 0
