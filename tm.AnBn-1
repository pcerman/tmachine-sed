#n

# Accepts tape: a{n}b{n}
# Example: echo 'aabb' | sed -rf tm.AnBn-1

# This source code is released under MIT License
# Copyright (c) 2016 Peter Cerman (https://github.com/pcerman)

/^>/ !s/^/>/
/#$/ !s/$/#/


#---------------------------------------------------------------------
:S0
#---------------------------------------------------------------------
p

s/>a/#>/
t S1

s/>#/#>/
t S4

b error

#---------------------------------------------------------------------
:S1
#---------------------------------------------------------------------
p

s/>a/a>/
t S1

s/>b/b>/
t S1

s/(.)>#/>\1#/
t S2

b error

#---------------------------------------------------------------------
:S2
#---------------------------------------------------------------------
p

s/(.)>b/>\1#/
t S3

b error

#---------------------------------------------------------------------
:S3
#---------------------------------------------------------------------
p

s/(.)>([ab])/>\1\2/
t S3

s/>#/#>/
t S0

b error

#---------------------------------------------------------------------
:S4
#---------------------------------------------------------------------
p

s/>#/#>/
t S5

/>$/ b halt

b error

#---------------------------------------------------------------------
:S5
#---------------------------------------------------------------------
p

s/>#/#>/
t halt

/>$/ b halt

b error

#---------------------------------------------------------------------

:error
i \
\
ERROR - invalid tape
p
q 1


:halt
i\
\
accepted
p
q 0
