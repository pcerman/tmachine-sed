#n

# Accepts tape: binary and operation
# Example: echo '1100#0110' | sed -rf tm.and-1

# This source code is released under MIT License
# Copyright (c) 2016 Peter Cerman (https://github.com/pcerman)

/^>/ !s/^/>/
/#$/ !s/$/#/


#-------------------------------------------------------
:S1
#-------------------------------------------------------
p

/>1/ {
     s/>1/i>/
     b S2
}

/>0/ {
     s/>0/o>/
     b S3
}

b error


#-------------------------------------------------------
:S2
#-------------------------------------------------------
p

/>[10]/ {
     s/>(.)/\1>/
     b S2
}

/>#/ {
     s/>#/#>/
     b S4
}

b error


#-------------------------------------------------------
:S3
#-------------------------------------------------------
p

/>[10]/ {
     s/>(.)/\1>/
     b S3
}

/>#/ {
     s/>#/#>/
     b S5
}

b error


#-------------------------------------------------------
:S4
#-------------------------------------------------------
p

/>[io]/ {
     s/>(.)/\1>/
     b S4
}

/>1/ {
     s/>1/i>/
     b S6
}

/>0/ {
     s/>0/o>/
     b S7
}

b error


#-------------------------------------------------------
:S5
#-------------------------------------------------------
p

/>[io]/ {
     s/>(.)/\1>/
     b S5
}

/>1/ {
     s/>1/i>/
     b S7
}

/>0/ {
     s/>0/o>/
     b S7
}

b error


#-------------------------------------------------------
:S6
#-------------------------------------------------------
p

/>[10]/ {
     s/>(.)/\1>/
     b S6
}

/>#/ {
     s/>#/#>/
     b S8
}

b error


#-------------------------------------------------------
:S7
#-------------------------------------------------------
p

/>[10]/ {
     s/>(.)/\1>/
     b S7
}

/>#/ {
     s/>#/#>/
     b S9
}

b error


#-------------------------------------------------------
:S8
#-------------------------------------------------------
p

/>[10]/ {
     s/>(.)/\1>/
     b S8
}

/>$/ {
     s/$/1/
     b S10
}

b error


#-------------------------------------------------------
:S9
#-------------------------------------------------------
p

/>[10]/ {
     s/>(.)/\1>/
     b S9
}

/>$/ {
     s/$/0/
     b S10
}

b error


#-------------------------------------------------------
:S10
#-------------------------------------------------------
p

/.>[10]/ {
     s/(.)>(.)/>\1\2/
     b S10
}

/.>#/ {
     s/(.)>#/>\1#/
     b S11
}

b error


#-------------------------------------------------------
:S11
#-------------------------------------------------------
p

/.>[10]/  b S12
/.>[io]/ {
     s/>(.)/\1>/
     b accept
}

b error


#-------------------------------------------------------
:S12
#-------------------------------------------------------
p

/.>[10io]/ {
     s/(.)>(.)/>\1\2/
     b S12
}

/.>#/ {
     s/(.)>(.)/>\1\2/
     b S13
}

b error


#-------------------------------------------------------
:S13
#-------------------------------------------------------
p

/.>[10]/  b S14
/.>[io]/ {
     s/>(.)/\1>/
     b accept
}

b error


#-------------------------------------------------------
:S14
#-------------------------------------------------------
p

/.>[10]/ {
     s/(.)>(.)/>\1\2/
     b S14
}

/>[io]/ {
     s/>(.)/\1>/
     b S1
}

b error


#-------------------------------------------------------
:accept

a\
\
accepted

b end

#-------------------------------------------------------
:error

a\
\
invalid input

:end
