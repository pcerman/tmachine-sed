#

# Accepts tape: decimal number
# echo '3' | sed -rf tm.cnt10

# This source code is released under MIT License
# Copyright (c) 2016 Peter Cerman (https://github.com/pcerman)

/>.*>/ b end

/^>/ !s/^/>/
/#$/ !s/$/#/


#-----------------------------------------------------------------------
:S1
p
#-----------------------------------------------------------------------
/>[0-9]/ {
         s/>(.)/\1>/
         b S1
}

/.>#/ {
     s/(.)>/>\1/
     b S2
}

b error


#-----------------------------------------------------------------------
:S2
p
#-----------------------------------------------------------------------

/>9/ { s/>./>8/; b S3}
/>8/ { s/>./>7/; b S3}
/>7/ { s/>./>6/; b S3}
/>6/ { s/>./>5/; b S3}
/>5/ { s/>./>4/; b S3}
/>4/ { s/>./>3/; b S3}
/>3/ { s/>./>2/; b S3}
/>2/ { s/>./>1/; b S3}
/>1/ { s/>./>0/; b S3}

/[0-9]>0/ { s/(.)>0/>\19/; b S2}

/>0/ { s/>0/>9/; b accept}

b error


#-----------------------------------------------------------------------
:S3
p
#-----------------------------------------------------------------------
s/$/x/
b S1

b error


#-----------------------------------------------------------------------
:accept

a\
\
ACCEPTED

b end

#-----------------------------------------------------------------------
:error

a\
\
invalid input

:end
